plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyPluginVersion}"
    id "me.champeau.jmh" version "${jmhPluginVersion}"
    id "com.adarshr.test-logger" version "${testsLoggerPluginVersion}"
}

bootJar {
    enabled = false
}

jar {
    enabled = false
}

apply from: "${project.projectDir}/libraries.gradle"

allprojects {
    group = "${projectBasePackage}"
    version = "${projectVersion}"

    repositories {
        mavenCentral()
    }

    compileJava {
        options.compilerArgs += [
                '-Amapstruct.defaultComponentModel=spring',
                '-Amapstruct.unmappedTargetPolicy=IGNORE'
        ]
    }

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.release = 17
        }
    }
}

subprojects {
    apply {
        plugin("java")
        plugin("groovy")
        plugin("org.springframework.boot")
        plugin("io.spring.dependency-management")
        plugin("me.champeau.jmh")
        plugin("com.adarshr.test-logger")
    }

    dependencies {
        implementation libs.springBootStarterTest
        testImplementation libs.spockCore
        testImplementation libs.spockSpring
        testImplementation libs.jmhCore
        testImplementation libs.jmhAnnotationProcesor
        jmhAnnotationProcessor libs.jmhAnnotationProcesor
    }

    test {
        useJUnitPlatform()
    }
}